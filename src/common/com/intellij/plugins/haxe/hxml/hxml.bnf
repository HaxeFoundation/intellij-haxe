{
  parserClass="com.intellij.plugins.haxe.hxml.HXMLParser"

  extends="com.intellij.extapi.psi.ASTWrapperPsiElement"

  psiClassPrefix="HXML"
  psiImplClassSuffix="Impl"
  psiPackage="com.intellij.plugins.haxe.hxml.psi"
  psiImplPackage="com.intellij.plugins.haxe.hxml.psi.impl"

  elementTypeHolderClass="com.intellij.plugins.haxe.hxml.psi.HXMLTypes"
  elementTypeClass="com.intellij.plugins.haxe.hxml.psi.HXMLElementType"
  tokenTypeClass="com.intellij.plugins.haxe.hxml.psi.HXMLTokenType"

  generate=[names="short"]
}

simpleFile ::= line_* unterminated_line_?

hxml ::= HXML_FILE
qualifiedName ::= QUALIFIEDCLASSNAME {methods=[getQName]}
option ::= KEY_TOKEN
value ::= VALUE_TOKEN | QUALIFIEDCLASSNAME
private eol_ ::= CRLF

private unterminated_line_ ::= option_
private line_ ::= option_? eol_
private line_recovery ::= !(eol_)
private option_ ::= (lib | define | classpath | main | property | COMMENT)  {recoverWhile="line_recovery"}

property ::= option value? {pin=1}
define ::= '-D' value {pin=1}
lib ::= '-lib' value (':' value)? {pin=1}
classpath ::= '-cp' value {pin=1}
main ::= '-main' qualifiedName {pin=1}

